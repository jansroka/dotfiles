#!/usr/bin/env zsh

# Load the shell dotfiles
for file in ~/.{exports,aliases,prompt}; do
	# shellcheck disable=SC1090
	[ -r "$file" ] && [ -f "$file" ] && source "$file";
done;
unset file;

# rbenv lazy initialization
_rbenv_loaded=0

_init_rbenv() {
  if [ $_rbenv_loaded -eq 0 ]; then
    eval "$(rbenv init -)"
    _rbenv_loaded=1
  fi
}

rbenv() {
  _init_rbenv
  command rbenv "$@"
}

# pyenv lazy initialization
_pyenv_loaded=0

_init_pyenv() {
  if [ $_pyenv_loaded -eq 0 ]; then
    eval "$(pyenv init -)"
    _pyenv_loaded=1
  fi
}

pyenv() {
  _init_pyenv
  command pyenv "$@"
}

# NVM - Load on every shell start
export NVM_DIR="$HOME/.nvm"

# Determine brew prefix without calling brew directly
if [ -d /opt/homebrew ]; then
  _BREW_PREFIX=/opt/homebrew
else
  _BREW_PREFIX=/usr/local
fi

[ -s "$_BREW_PREFIX/opt/nvm/nvm.sh" ] && \. "$_BREW_PREFIX/opt/nvm/nvm.sh"
[ -s "$_BREW_PREFIX/opt/nvm/etc/bash_completion.d/nvm" ] && \. "$_BREW_PREFIX/opt/nvm/etc/bash_completion.d/nvm"

# Automatically use nvm when entering directories with .nvmrc
autoload -U add-zsh-hook
load-nvmrc() {
  if [ -f .nvmrc ]; then
    local node_version="$(nvm version)"
    local nvmrc_node_version=$(nvm version "$(cat .nvmrc)")

    if [ "$nvmrc_node_version" = "N/A" ]; then
      nvm install
    elif [ "$nvmrc_node_version" != "$node_version" ]; then
      nvm use
    fi
    hash -r
  fi
}
add-zsh-hook chpwd load-nvmrc

# Basic Memory - Use Python 3.13 to avoid Pydantic warnings
bm() { uvx --python python3.13 basic-memory "$@"; }

# Load acme.sh environment if it exists
if [ -f ~/.acme.sh/acme.sh.env ]; then
  . ~/.acme.sh/acme.sh.env
fi

# direnv - automatically load .envrc files
eval "$(direnv hook zsh)"

# Deduplicate PATH entries (must be done after all PATH modifications)
export PATH="${(j/:/)${(@u)${(s/:/)PATH}}}"
